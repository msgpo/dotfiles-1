
pkgname=kwin-customized
pkgver=5.11.4
pkgrel=3
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kscreenlocker
         xcb-util-cursor
         plasma-framework
         kcmutils
         breeze
         kinit)
makedepends=(extra-cmake-modules
             qt5-tools
             kdoctools
             python)
optdepends=('qt5-virtualkeyboard: virtual keyboard support for kwin-wayland')
provides=(kwin)
conflicts=(kwin)
source=("https://download.kde.org/stable/plasma/$pkgver/kwin-$pkgver.tar.xz"{,.sig}
        allow-shadows-for-maximized-windows.patch
        add-jinni-effect.patch)
sha256sums=('f989b2dbfd79d63e19e2bf1beb9e9088a3fb9a4a0d4a3ceac2b42201bd15d751'
            'SKIP'
            '4579993b4d7edfe3365bf2b7fe459573cf11d94282acc4fa81b9b8e4d70d14cc'
            'd2f077d2b6417f7abb7ea701bed3767b58dbbf521f4733c56079357548fa2c0a')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

  cd "kwin-$pkgver"
  patch -Np1 -i "${srcdir}/allow-shadows-for-maximized-windows.patch"
  patch -Np1 -i "${srcdir}/add-jinni-effect.patch"
}

build() {
  cd build
  cmake ../kwin-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
