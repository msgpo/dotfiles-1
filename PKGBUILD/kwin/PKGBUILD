
pkgname=kwin-patched
_pkgname=kwin
pkgver=5.11.5
pkgrel=1
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kscreenlocker
         xcb-util-cursor
         plasma-framework
         kcmutils
         breeze
         kinit)
makedepends=(extra-cmake-modules
             qt5-tools
             kdoctools
             python)
optdepends=('qt5-virtualkeyboard: virtual keyboard support for kwin-wayland')
provides=(kwin)
conflicts=(kwin)
source=("https://download.kde.org/stable/plasma/${pkgver}/${_pkgname}-${pkgver}.tar.xz"{,.sig}
        add-jinni-effect.patch
        add-slidedesktops-effect.patch)
sha256sums=('9fb0db84fd13568a0dcae7d1fdd3e72e5bbb80120d374941043d4474ea167f19'
            'SKIP'
            'd2f077d2b6417f7abb7ea701bed3767b58dbbf521f4733c56079357548fa2c0a'
            'f55b158db549e1aad9b939fa6e6be70c61ec5e4cbb066c95276a287a3aaad466')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

  cd "${_pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/add-jinni-effect.patch"
  patch -Np1 -i "${srcdir}/add-slidedesktops-effect.patch"
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
